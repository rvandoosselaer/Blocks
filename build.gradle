plugins {
    id 'idea'
    id 'java-library'
}

group = "com.rvandoosselaer"
version = "1.7.1-SNAPSHOT"
// The artifact ID defaults to the project name configured in settings.gradle

sourceCompatibility = 1.8

ext {
    jmeVersion = "[3.4,)"
    lombokVersion = "1.18.22"
    slf4jVersion = "2.0.3"
    simMathVersion = "[1.4,)"
    junitVersion = "[5.8.0,)"
    mockitoVersion = "[4.2,)"
    caffeineVersion = "2.8.8"
    protobufVersion = "[4.0,)"
    guavaVersion = "[31,)"
    minieVersion = "[4.6,)"
    jacksonVersion = "[2.13,)"
}

repositories {
    mavenCentral()
}

dependencies {
    // JME
    implementation "org.jmonkeyengine:jme3-core:${jmeVersion}"
    implementation "org.jmonkeyengine:jme3-effects:${jmeVersion}"

    // physics
    implementation "com.github.stephengold:Minie:${minieVersion}"

    // utilities
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    // api exposes the library to consumers
    api "com.simsilica:sim-math:${simMathVersion}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"

    // logging
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:jul-to-slf4j:${slf4jVersion}"

    // testing
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.jmonkeyengine:jme3-core:${jmeVersion}"
    testImplementation "org.jmonkeyengine:jme3-desktop:${jmeVersion}"
}

test {
    useJUnitPlatform()
}

// copy the generated javadocs into the /docs folder for the github pages
task copyJavadoc(dependsOn: javadoc) {
    doLast {
        copy {
            from "$buildDir/docs"
            into "$projectDir/docs"
        }
    }
}

// remove the generated javadocs in the /docs folder
task cleanJavadoc(type: Delete) {
    delete "$projectDir/docs/javadoc/"
}

clean {
    dependsOn cleanJavadoc
}

jar {
    exclude 'blocks.proto'
}

build {
    dependsOn copyJavadoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

javadoc {
    failOnError = false
}